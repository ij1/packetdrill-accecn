// Test SACK space grab to fit AccECN option
--tcp_ts_tick_usecs=1000

`../../common/defaults.sh
sysctl -q net.ipv4.tcp_ecn=5
`

    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
   +0 bind(3, ..., ...) = 0
   +0 listen(3, 1) = 0

   +0 < SEWA 0:0(0) win 32792 <mss 1050,sackOK,TS val 1 ecr 0,nop,wscale 8>
+.002 > SW. 0:0(0) ack 1 <mss 1460,sackOK,TS val 100 ecr 1,AccECNEXP0 e0b 1 ceb 0 e1b 0,wscale 8>
+0.05 < [ect0] W. 1:1(0) ack 1 win 264 <nop,nop,TS val 2 ecr 100,AccECNEXP0 e0b 1 ceb 0 e1b 0,nop,nop,nop>
+.002 accept(3, ..., ...) = 4

+.01 < [ect1] EAP. 1001:2001(1000) ack 1 win 264 <nop,nop,TS val 3 ecr 100>
+0.002 > [ect0] EA. 1:1(0) ack 1 <nop,nop,TS val 160 ecr 2,AccECNEXP0 e0b 1 ceb 0 e1b 1000,nop,sack 1001:2001>
+.01 < [ect0] EAP. 3001:4001(1000) ack 1 win 264 <nop,nop,TS val 4 ecr 100>
+0.002 > [ect0] EA. 1:1(0) ack 1 <nop,nop,TS val 172 ecr 2,AccECNEXP0 e0b 1001 ceb 0,sack 3001:4001 1001:2001>
+.01 < [ce] EAP. 5001:6001(1000) ack 1 win 264 <nop,nop,TS val 5 ecr 100>
+0.002 > [ect0] WA. 1:1(0) ack 1 <nop,nop,TS val 184 ecr 2,AccECNEXP0 e0b 1001 ceb 1000,sack 5001:6001 3001:4001>
// DSACK works?
+.01 < [ect0] EAP. 5001:6001(1000) ack 1 win 264 <nop,nop,TS val 5 ecr 100>
+0.002 > [ect0] WA. 1:1(0) ack 1 <nop,nop,TS val 196 ecr 2,AccECNEXP0 e0b 2001 ceb 1000,sack 5001:6001 5001:6001>
// Option doesn't fit?
+.01 < [ect1] EAP. 6001:7001(1000) ack 1 win 264 <nop,nop,TS val 5 ecr 100>
+0.002 > [ect0] WA. 1:1(0) ack 1 <nop,nop,TS val 204 ecr 2,nop,nop,sack 5001:7001 3001:4001 1001:2001>
