// Test false overflow detection with option used to rule out overflow

`../../common/defaults.sh
sysctl -q net.ipv4.tcp_ecn=3
`

    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
   +0 bind(3, ..., ...) = 0
   +0 listen(3, 1) = 0

   +0 < SEWA 0:0(0) win 32792 <mss 1460,nop,nop,sackOK,nop,wscale 8>
+.002 > SW. 0:0(0) ack 1 <mss 1460,AccECNEXP0 e0b 1 ceb 0 e1b 0,nop,sackOK,nop,wscale 8>
+0.05 < [ect0] W. 1:1(0) ack 1 win 264 <AccECNEXP0 e0b 1 ceb 0 e1b 0,nop,nop,nop>
+.002 accept(3, ..., ...) = 4

+0.01 %{ assert tcpi_delivered_ce == 0, tcpi_delivered_ce }%

// Stop sending option to allow easier testing
+0 `sysctl -q net.ipv4.tcp_ecn_option=0`

+0.002 write(4, ..., 14600) = 14600
+.002 > [ect0] P.5 1:14601(14600) ack 1

+0.05 < [ect0] .5 1:1(0) ack 1460 win 264 <AccECNEXP0 e0b 1461 ceb 0 e1b 0,nop,nop,nop>
+0.05 < [ect0] .5 1:1(0) ack 14601 win 264 <AccECNEXP0 e0b 14601 ceb 0 e1b 0,nop,nop,nop>

+0.01 %{
assert tcpi_delivered_ce == 0, tcpi_delivered_ce
assert tcpi_delivered_e0_bytes == 14600, tcpi_delivered_e0_bytes
}%
